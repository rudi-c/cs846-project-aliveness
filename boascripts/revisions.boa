# Get the date and author of every revision of every relevant project.
p: Project = input;
# Output (use only one letter to reduce size of output)
o: output collection[string][string] of string;

visit(p, visitor {
    before n: Project ->
        # Only want Java projects
        ifall (i: int; !match(`^java$`, lowercase(n.programming_languages[i])))
            stop;
    before repo: CodeRepository ->
        if (len(repo.revisions) > 0) {
            lastindex := len(repo.revisions) - 1;
            lastyear := yearof(repo.revisions[lastindex].commit_date);
            if (lastyear > 2013)
                stop;
        }
    before rev: Revision -> {
        # http://stackoverflow.com/questions/18134627
        # The largest projects need ~12 characters/40 to stay unique
        shorthash := substring(rev.id, 0, 15);
        data := format("%s|%s",
                       formattime("%d/%m/%y %H:%M:%S", rev.commit_date),
                       rev.author.username
                      );
        o[p.id][shorthash] << data;
    }
});
