# Get relevant information for projects.
p: Project = input;
# Output (use only one letter to reduce size of output)
o: output collection[string] of string;


visit(p, visitor {
    before n: Project ->
        # Only want Java projects
        ifall (i: int; !match(`^java$`, lowercase(n.programming_languages[i])))
            stop;
    before repo: CodeRepository -> {
        if (len(repo.revisions) > 0) {
            lastindex := len(repo.revisions) - 1;
            lastyear := yearof(repo.revisions[lastindex].commit_date);
            if (lastyear > 2013)
                stop;
        } else {
            stop;
        }

        # Look for for certain documentation-type files.
        # Pack results in an int so that the output uses less characters.
        documentation := 0;
        files := getsnapshot(repo);
        foreach (i: int; def(files[i])) {
            fname := lowercase(files[i].name);
            if (match(`.*readme($|\.md$|\.txt$)`, fname))
                documentation = documentation + 1;
            if (match(`.*license($|\.md$|\.txt$)`, fname))
                documentation = documentation + 2;
            if (match(`.*todo($|\.md$|\.txt$)`, fname))
                documentation = documentation + 4;
            if (match(`.*install($|\.md$|\.txt$)`, fname))
                documentation = documentation + 8;
            if (match(`.*contributing($|\.md$|\.txt$)`, fname))
                documentation = documentation + 16;
            if (match(`.*changelog($|\.md$|\.txt$)`, fname))
                documentation = documentation + 32;
        }

        data := format("%s|%d|%d|%s|%d|%d",
                       p.name,
                       len(repo.revisions),
                       documentation,
                       formattime("%d/%m/%y %H:%M:%S", p.created_date),
                       len(p.description),
                       len(p.programming_languages));
        o[p.id] << data;
    }
});
