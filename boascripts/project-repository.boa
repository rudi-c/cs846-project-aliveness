# Get the date and author of every revision of every relevant project.
p: Project = input;
# Output (use only one letter to reduce size of output)
o: output collection[string] of string;

visit(p, visitor {
    before n: Project ->
        # Only want Java projects
        ifall (i: int; !match(`^java$`, lowercase(n.programming_languages[i])))
            stop;
    before repo: CodeRepository -> {
        if (len(repo.revisions) > 0) {
            lastindex := len(repo.revisions) - 1;
            lastyear := yearof(repo.revisions[lastindex].commit_date);
            if (lastyear > 2013)
                stop;
        } else {
            stop;
        }

        # Look for a README file
        # Store as int instead of bool so that the output uses less characters.
        has_readme := 0;
        files := getsnapshot(repo);
        foreach (i: int; def(files[i])) {
            fname := lowercase(files[i].name);
            if (fname == "readme.md" || fname == "readme.txt" || fname == "readme") {
                has_readme = 1;
            }
        }

        data := format("%s|%d|%d|%s|%d|%d",
                       p.name,
                       len(repo.revisions),
                       has_readme,
                       formattime("%d/%m/%y %H:%M:%S", p.created_date),
                       len(p.description),
                       len(p.programming_languages));
        o[p.id] << data;
    }
});
